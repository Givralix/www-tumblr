#!/usr/bin/env perl

use Mojolicious::Lite;
use WWW::Tumblr;

my $blog = 'blog.tumblr.com';

my $tumblr = WWW::Tumblr->new(
	consumer_key => 'xxx',
	secret_key => 'xxx',
	blog => $blog,
	callback => 'http://my-application-site.com/callback'
);

get '/' => sub {
  my $self = shift;
  $self->render('index');
};

get '/token' => sub {
	my $self = shift;
	$self->redirect_to( $tumblr->authorization_url );
};

get '/callback' => sub {
	my $self = shift;
	$tumblr->get_token( $self->param('oauth_token'), $self->param('oauth_verifier') )
		if $self->param('oauth_token') && $self->param('oauth_verifier');	#	Net::OAuth::Client will die ovewise
	$self->redirect_to('/post');
};

get '/post' => sub {
	my $self = shift;
	$self->render('post');
};

post '/post' => sub {
	my $self = shift;

	my $result = $tumblr->post( type => 'text', title => 'test', body => $self->param('post_text') || 'buzz' );
	if ( $result && $result->{response} && $result->{response}->{id} ) {
		return $self->redirect_to("http://$blog/post/$result->{response}->{id}")
	}

	$self->render( text => $tumblr->error );
};

app->start;

__DATA__

@@ index.html.ep
% layout 'default';
% title 'Tumblr Post Test';
<a href='/token'>Get Token</a><br>
<a href='/post'>Post something</a><br>

@@ post.html.ep
% layout 'default';
% title 'Tumblr Post Test';
<form method='post'>
	<label for='post_text'>Some post:</label><br>
	<textarea id='post_text' name='post_text'></textarea><br>
	<input type='submit'>
</form>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
</html>
